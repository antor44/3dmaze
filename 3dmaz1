5 REM *** 3D MAZE PRO 1.06***
10 REM *** New remastered version by antor44.Original author unknown (Abandonware) ***
20 COLOR 15,1,1:SCREEN 2,,0:WW=RND(-TIME):OPEN "GRP:" FOR OUTPUT AS #1:PRESET(84,90):PRINT#1,"3D MAZE PRO"
30 DEFINT D-Z:DIM A%(18,18,4),B%(18,18),E%(3,1):DI=2:RO=3:LE=4:E%(0,0)=206:E%(1,0)=207:E%(2,0)=205:E%(3,0)=208:E%(0,1)=-2:E%(1,1)=2:E%(2,1)=2:E%(3,1)=-2
35 FOR I=1 TO 300:NEXT I:FOR I=0 TO 9:READ C$(I),D$(I):NEXT I:FG$(4)="E":FG$(3)="X":FG$(2)="I":FG$(1)="T":OG$(4)="O":OG$(3)="U":OG$(2)="C":OG$(1)="H"
40 CLS:COLOR 15,1,1:DEFUSR=&H90:A=USR(0)
50 PRESET(56,60):PRINT#1,"Start LEVEL 1-9";"(*";CHR$(48+INT(RO/2));")":RO$=INPUT$(1):IF RO$=CHR$(13) AND RO<=18 AND RO>=3 THEN 60 ELSE RO=VAL(RO$)*2:IF RO>18 OR RO<2 THEN PLAY "V8T250S13M50L8O2CCC":GOTO 50
60 PRESET(52,90):PRINT#1,"Visual Helps 0-9";"(*";CHR$(48+LE);")":LE$=INPUT$(1):IF LE$=CHR$(13) AND LE<=9 AND LE>=0 THEN 70 ELSE LE=VAL(LE$):IF LE$>"9" OR LE$<"0" THEN PLAY "V8T250S13M50L8O2CCC":GOTO 60
70 PRESET(66,120):PRINT#1,"Sound Help *Y/N":PI$=INPUT$(1):IF PI$="Y" OR PI$="y" OR PI$=CHR$(13) THEN PI=1 ELSE PI=0
80 PRESET(6,150):PRINT#1,"Difficult (to pass past rooms):":PRESET(30,160)
85 PRINT#1,"1=Easy,2=Medium,3=Hard(*";CHR$(48+DI);")":DI$=INPUT$(1):IF DI$=CHR$(13) AND DI<=3 AND DI>=1 THEN 90 ELSE DI=VAL(DI$):IF DI$<"1" OR DI$>"3" THEN PLAY "V8T250S13M50L8O2CCC":GOTO 80
90 CLS:COLOR 15,1,1:PRESET(100,70):PRINT#1,"LEVEL";INT(RO/2)
95 PRESET(60,120):PRINT#1,"Generating Maze...":KO=0:SA=0:BC=15:IF RO=2 THEN RO=3
100 FOR M=0 TO RO
105 FOR N=0 TO RO
110 B%(M,N)=12-DI*2
115 NEXT N,M
120 FOR N=1 TO RO
130 A%(N,RO,1)=1:A%(N,1,3)=1
140 A%(1,N,4)=1:A%(RO,N,2)=1
150 NEXT
160 A=RND(1):B=RND(1):C=RND(1)
170 IF A>.5 THEN 210
180 IF B>.8 THEN 200
190 A%(1+INT((RO-1)*C),1,3)=2:XS=1+INT((RO-1)*C):YS=1:GOTO 240
200 A%(1+INT((RO-1)*C),RO,1)=2:XS=1+INT((RO-1)*C):YS=RO:GOTO 240
210 IF B>.8 THEN 230
220 A%(1,1+INT((RO-1)*C),4)=2:XS=1:YS=1+INT((RO-1)*C):GOTO 240
230 A%(RO,1+INT((RO-1)*C),2)=2:XS=RO:YS=1+INT((RO-1)*C)
240 REM MAZE
250 FOR M=1 TO RO-1
260 FOR N=1 TO RO-1
270 IF RND(1)>.5 THEN 310
280 A%(M,N,2)=1:A%(M+1,N,4)=1
290 A%(M,N,1)=0:A%(M,N+1,3)=0
300 GOTO 330
310 A%(M,N,2)=0:A%(M+1,N,4)=0
320 A%(M,N,1)=1:A%(M,N+1,3)=1
330 NEXT
340 NEXT
350 REM PLAYER INIT POSITION
360 X=INT(RND(1)*RO)+1:Y=INT(RND(1)*RO)+1:IF (ABS(X-XS)+1)<(RO/3) OR (ABS(Y-YS)+1)<(RO/3) THEN 360
370 EST=0:XU=0:YU=1:A=1:B=2:C=4:GOSUB 7000:CLS
380 IF PI=1 THEN PLAY"V0O"+STR$(8-INT(ABS(XS-X)/((RO+.1)/8)))+"L64G"
385 IF A%(X,Y,A)=1 THEN 430
390 IF A%(X,Y,B)=1 THEN 420
400 IF A%(X,Y,C)=1 THEN 2500
410 GOTO 2000
420 IF A%(X,Y,C)=1 THEN 3500 ELSE 3000
430 IF A%(X,Y,B)=1 THEN 450
440 IF A%(X,Y,C)=1 THEN 4500 ELSE 4000
450 IF A%(X,Y,C)=1 THEN 5500 ELSE 5000
460 REM MAIN LOOP
470 D=STICK(0)
475 IF PI=1 THEN SOUND 8,10-ABS((YS-Y)/2)
480 IF D=1 THEN 620
490 IF D=3 THEN 540
500 IF D=7 THEN 560
510 IF STRIG(0) THEN GOSUB 7000:CLS:GOTO 380
520 GOTO 470
530 REM PLAYER MOVEMENT
540 IF EST<3 THEN EST=EST+1 ELSE EST=0
550 GOTO 570
560 IF EST>0 THEN EST=EST-1 ELSE EST=3
570 REM ROOM MOVEMENT
580 IF EST=0 THEN GOSUB 6000:GOTO 900
590 IF EST=1 THEN GOSUB 6100:GOTO 900
600 IF EST=2 THEN GOSUB 6200:GOTO 900
610 XU=-1:YU=0:A=4:B=1:C=3:GOTO 900
620 X1=X+XU:Y1=YU+Y
630 IF A%(X,Y,A)=1 THEN PLAY "V8T250S13M50L8O2CCC":X1=X:Y1=Y:GOTO 900
640 IF A%(X,Y,A)=2 THEN SA=1:GOSUB 8100:GOTO 9000
650 X=X1:Y=Y1:B%(X,Y)=B%(X,Y)-1
660 IF B%(X,Y)=0 THEN GOSUB 8100:GOTO 9500
670 IF B%(X,Y)=1 THEN GOSUB 8100:BC=8:COLOR BC:GOTO 380
680 IF B%(X,Y)=2 THEN GOSUB 8100:BC=10:COLOR BC:GOTO 380
690 IF B%(X,Y)=3 THEN GOSUB 8100:BC=2:COLOR BC:GOTO 380
700 GOSUB 8100:BC=15:COLOR BC
900 GOTO 380
2000 GOSUB 8000
2010 LINE(45,162)-STEP(0,-97):LINE-STEP(-25,-10):LINE-STEP(0,117)
2020 LINE(100,150)-STEP(0,-85):LINE-STEP(40,0):LINE-STEP(0,85)
2030 LINE(195,162)-STEP(0,-97):LINE-STEP(25,-10):LINE-STEP(0,117)
2040 GOTO 470
2500 GOSUB 8000
2510 LINE(100,150)-STEP(0,-85):LINE-STEP(40,0):LINE-STEP(0,85)
2520 LINE(195,162)-STEP(0,-97):LINE-STEP(25,-10):LINE-STEP(0,117)
2530 LINE(100,150)-STEP(0,-85):LINE-STEP(40,0):LINE-STEP(0,85)
2550 GOTO 470
3000 GOSUB 8000
3010 LINE(45,162)-STEP(0,-97):LINE-STEP(-25,-10):LINE-STEP(0,117)
3020 LINE(100,150)-STEP(0,-85):LINE-STEP(40,0):LINE-STEP(0,85)
3030 GOTO 470
3500 GOSUB 8000
3510 LINE(100,150)-STEP(0,-85):LINE-STEP(40,0):LINE-STEP(0,85)
3520 GOTO 470
4000 GOSUB 8000
4010 LINE(45,162)-STEP(0,-97):LINE-STEP(-25,-10):LINE-STEP(0,117)
4020 LINE(195,162)-STEP(0,-97):LINE-STEP(25,-10):LINE-STEP(0,117)
4030 GOTO 470
4500 GOSUB 8000
4510 LINE(195,162)-STEP(0,-97):LINE-STEP(25,-10):LINE-STEP(0,117)
4520 GOTO 470
5000 GOSUB 8000
5010 LINE(45,162)-STEP(0,-97):LINE-STEP(-25,-10):LINE-STEP(0,117)
5020 GOTO 470
5500 GOSUB 8000
5510 GOTO 470
6000 XU=0:YU=1:A=1:B=2:C=4:RETURN
6100 XU=1:YU=0:A=2:B=3:C=1:RETURN
6200 XU=0:YU=-1:A=3:B=4:C=2:RETURN
7000 CLS:KO=KO+1:IF KO>LE THEN PLAY "V8T250S13M50L8O2CCC":RETURN ELSE COLOR 15
7010 FOR M=1 TO RO
7020 FOR N=1 TO RO
7030 IF A%(M,N,3)<>1 THEN 7050
7040 LINE((170/RO)*(M-1)+49,172-(170/RO)*(N-1))-STEP((170/RO),0)
7050 IF A%(M,N,2)<>1 THEN 7070
7060 LINE((170/RO)*(M-1)+49+(170/RO),172-(170/RO)*(N-1))-STEP(0,-(170/RO))
7070 NEXT
7080 NEXT
7090 FOR M=1 TO RO
7100 IF A%(1,M,4)=2 THEN 7120
7110 LINE(49,172-(170/RO)*(M-1))-STEP(0,-(170/RO))
7120 IF A%(M,RO,1)=2 THEN 7140
7130 LINE((170/RO)*(M-1)+49,2)-STEP((170/RO),0)
7140 NEXT
7150 IF E%(EST,0)<207 THEN PRESET((170/RO)*(X-1)+46+(170/RO/2),169-(170/RO)*(Y-1)-(170/RO/2)+E%(EST,1)) ELSE PRESET((170/RO)*(X-1)+46+(170/RO/2)+E%(EST,1),169-(170/RO)*(Y-1)-(170/RO/2))
7160 PRINT#1,CHR$(E%(EST,0))
7170 FOR FR=1 TO 500:NEXT:COLOR BC:RETURN
8000 CLS
8010 LINE(0,20)-STEP(74,30):LINE-STEP(0,100):LINE-STEP(-70,30)
8020 LINE(75,50)-STEP(89,0):LINE-STEP(0,100):LINE-STEP(-90,0)
8030 LINE(239,20)-STEP(-74,30):LINE(165,150)-STEP(74,30)
8040 RETURN
8100 LINE(100,150)-STEP(40,0),1:W=BC
8110 LINE(140,150)-STEP(-35,10),W:LINE-STEP(0,-85),W:LINE-STEP(35,-10),W
8120 IF W=BC THEN W=1:GOTO 8110 ELSE W=BC
8130 LINE(140,65)-STEP(-25,10),W:LINE-STEP(0,85),W:LINE-STEP(25,-10),W
8140 IF W=BC THEN W=1:GOTO 8130 ELSE W=BC
8150 LINE(140,65)-STEP(-15,15),W:LINE-STEP(0,85),W:LINE-STEP(15,-15),W
8155 IF SA=1 THEN 8170
8160 IF W=BC THEN W=1:GOTO 8150
8170 RETURN
9000 A=USR(0):IF RO=3 THEN RO=4 ELSE IF RO<18 THEN RO=RO+2
9020 PLAY "T255O3AO4C#EF#GF#EC#T255O3EG#BO4C#DC#O3BG#T255O3BO4D#F#G#AG#F#D#T255O3AO4C#EF#GF#EC#"
9030 FOR I=1 TO 5:COLOR RND(1)*14+2
9040 FOR GF=93 TO 157 STEP 16
9050 PRESET(111,GF):PRINT#1,FG$((157-GF)/16)
9060 NEXT GF
9070 PRESET(60,180):PRINT#1,"CONGRATULATIONS"
9080 RO$=INKEY$:IF RO$=CHR$(13) THEN RO$=INKEY$:GOTO 90
9090 NEXT I
9250 COLOR RND(1)*14+2
9260 FOR GF=93 TO 157 STEP 16
9270 PRESET(110,GF):PRINT#1,FG$((157-GF)/16)
9280 NEXT GF
9290 PRESET(60,180):PRINT#1,"CONGRATULATIONS"
9300 IF PLAY(0) GOTO 9250 ELSE 90
9500 A=USR(0)
9510 PLAY C$(0),D$(0)
9520 FOR Z=1 TO 9
9530 PLAY C$(Z),D$(Z)
9540 FOR I=1 TO 5:COLOR RND(1)*14+2
9550 FOR GF=83 TO 147 STEP 16
9560 PRESET(118,GF):PRINT#1,OG$((147-GF)/16)
9570 NEXT GF
9580 PRESET(88,180):PRINT#1,"GAME OVER"
9590 RO$=INKEY$:IF RO$=CHR$(13) THEN RO$=INKEY$:GOTO 40
9600 NEXT I,Z
9610 KO=-1:GOSUB 7000:PLAY "L2GEL4CL8DD+EGL4AL1O3C"
9620 COLOR RND(1)*14+2
9630 IF E%(EST,0)<207 THEN PRESET((170/RO)*(X-1)+46+(170/RO/2),169-(170/RO)*(Y-1)-(170/RO/2)+E%(EST,1)) ELSE PRESET((170/RO)*(X-1)+46+(170/RO/2)+E%(EST,1),169-(170/RO)*(Y-1)-(170/RO/2))
9640 PRINT#1,CHR$(E%(EST,0))
9650 PRESET(98,180):PRINT#1,"GAME OVER"
9660 RO$=INKEY$:IF RO$=CHR$(13) THEN RO$=INKEY$:GOTO 40 ELSE 9620
9700 DATA "V8T250S13M50L4","V8T250S13M50L1"
9710 DATA "DF+GAR1DF+GAR4R2","R4R2DDD"
9720 DATA "DF+GA2F+2D2F+2ER4R2","DDDA"
9730 DATA "F+F+ED2.DF+2AAAG2.","ADDG"
9740 DATA "R4BBA2F+2D2E2D1","GDD2A2D"
9750 DATA "V12L4T255","V8"
9760 DATA "CEFL1GR6L4CEFL1G","V8"
9770 DATA "R6L4CEFL2GECEL1D","V8"
9780 DATA "R8L4EEDL2CL4CL2E","V8"
9790 DATA "L4GGGL1FL4EF","V8"
